;
; prj.asm
;
; Created: 2020/06/06 09:26:31
; Author : Nduvho
;		 : 1490804
;
;This file contains a program that monitors a 
;temperature and display it on 3 Seven segment displays

;====================================================================
; Main.asm file generated by New Project wizard
;
; Created:   Fri May 8 2020
; Processor: ATmega328P
; Compiler:  AVRASM (Proteus)
;====================================================================

;====================================================================
; DEFINITIONS
;====================================================================
.DEF HUNDREDS        = R12
.DEF TENS            = R13
.DEF ONES            = R14
.DEF MULTIPIER       = R15

.DEF TEMP            = R16
.DEF ADC_OUTH        = R17
.DEF ADC_OUTL        = R18
.DEF COUNTER         = R19
.DEF SEG_DISP        = R20
.DEF ORDER           = R21
.DEF TEMPERATURE     = R22
;====================================================================

;====================================================================
; RESET and INTERRUPT VECTORS
;====================================================================
;.INCLUDE "M328PDEF.INC"

; Reset Vector
.ORG 0x000
RJMP MAIN

.ORG 0x016
RJMP CONVERT  ; Timer1 Compare A Handler to do conversions on defined intervals

.ORG 0x001C 
RJMP WAKEUP   ; Timer0 Compare A Handler wake up when asleep
;====================================================================
; CODE SEGMENT
;====================================================================
;
; Created: 3/6/2020 9:12:16 PM
; Author : ramashian
.CSEG
;LOOK-UP TABLE FOR THE DIGITS(0-9)
.ORG 0x100
DIGITS:
.DB 0xFC, 0x60, 0xDA, 0xF2, 0x66, 0xB6, 0xBE, 0xE0, 0xFE, 0xF6

MAIN:
	CBI DDRC, 0
	LDI TEMP, 0xFF
	OUT DDRD, TEMP
	OUT DDRB, TEMP
	LDI ZH, HIGH(DIGITS)<<1	
	
	;SETTING UP THE ADC
	LDI TEMP, 1<<REFS0 
	STS ADMUX, TEMP
	; ENABLING AND SETTING THE PRESCALER
	LDI TEMP, (1<<ADEN) | (1<<ADSC) | (1<<ADPS2) | (1<<ADPS1)  
	STS ADCSRA, TEMP

	;THE TIMER/COUNTER PART	
	;LOADING THE COMPARE VALUE
	LDI TEMP, 0x3D
	STS OCR1AH, TEMP
	LDI TEMP, 0x09
	STS OCR1AL, TEMP
	
	; SETTING THE PRESCALER/ ACTIVATING ;;;;;, AND THE MODE(CTC)
	LDI TEMP, (1<<CS12) | (1<<CS10) | (1<<WGM12)
	STS TCCR1B, TEMP
	
	LDI TEMP, (1<<OCIE1A)
	STS TIMSK1, TEMP

	;A TIMER FOR HANDING THE DISPLAYS' 'ON'TIME DELAYS
	;SETTING THE PRESACALER AND CTC MODE TO ENABLE ADJUSTING TIME DELAYS
	LDI TEMP, (1<<WGM01)
	OUT TCCR0A, TEMP
	LDI TEMP, (1<<CS02)|(1<<CS00)
	OUT TCCR0B, TEMP
	;ENABLING OUTPUT COMPARE A INTERRUPT
	LDI TEMP, (1<<OCIE0A)
	STS TIMSK0, TEMP

	SEI
	
	;SETTING UP THE SLEEP REG.
    LDI TEMP, 1<<SE
    OUT SMCR, TEMP

	;(RE)SETING THE DELAYS BTWN DISPAYS
	LDI TEMP, 0x99
	OUT OCR0A, TEMP
	
LOOP:
	;===============================
	;DISPLAYING THE SEPERATED DIGITS
	MOV ORDER, HUNDREDS
	LDI SEG_DISP, 0b001
	RCALL DISPLAY
	
	MOV ORDER, TENS
	LDI SEG_DISP, 0b010
	RCALL DISPLAY
	
	MOV ORDER, ONES
	LDI SEG_DISP, 0b100
	RCALL DISPLAY
	;==============================
	RJMP LOOP
	
CONVERT:
	;SBI EQUIV / ENABLING THE ADC
	LDS TEMP, ADCSRA
	SBR TEMP, (1<<ADSC) | (1<<ADEN) 
    STS ADCSRA, TEMP
    NOP
	
	LDS ADC_OUTL, ADCL
	LDS ADC_OUTH, ADCH

	LDI TEMP, 125
	MUL ADC_OUTL, TEMP
	LDI TEMP, 50
	SUB R1, TEMP			;USING THE HIGH BYTE INSTEAD OF MULTIPLYING BY 256 
	
	; SPLIT THE TEMPERATURE TO DISPLAY THE SEPRT DIGITS
	MOV TEMPERATURE, R1
	RCALL SPLIT

	;SBI EQUIV / DISABLING THE ADC
	LDS TEMP, ADCSRA
	CBR TEMP, (1<<ADEN) 
    STS ADCSRA, TEMP
	    
	RETI

WAKEUP:
	RETI

DISPLAY:
	OUT PORTB, SEG_DISP
	ADD ZL, ORDER
	NOP 
	LPM TEMP, Z
	OUT PORTD, TEMP
	CLR ZL 
	SLEEP	;WAIT
	RET

;===============================================================================
;A SUBROUTINE TO SPLIT THE TEMPERATURE INTO SINGLE NUMBERS BETWEEN 0 AND 9   ;==
SPLIT:                                                                       ;==
	CLR HUNDREDS      ;RESETING THE DIGITS
	CLR TENS
	CLR ONES
	    
	CPI TEMPERATURE, 100
	BRLO TENS_ORDER
	INC HUNDREDS
	SUBI TEMPERATURE, 100
	    
TENS_ORDER:
	CPI TEMPERATURE, 10
	BRLO ONES_ORDER
	INC TENS
	SUBI TEMPERATURE, 10
	RJMP TENS_ORDER
	    
ONES_ORDER:
	MOV ONES, TEMPERATURE                                                      ;==
	                                                                           ;==
	RET	                                                                       ;==
;=================================================================================

